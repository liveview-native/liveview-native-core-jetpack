plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id "org.mozilla.rust-android-gradle.rust-android" version "0.9.3"
}

def githubPropertiesFile = rootProject.file("github.properties");
def githubProperties = new Properties()
githubProperties.load(new FileInputStream(githubPropertiesFile))

android {
    namespace 'org.phoenixframework.liveview_native_core_jetpack'
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "0.1.0-pre-alpha04"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false

        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    ndkVersion '21.4.7075529'


}

apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

cargo {
    module  = "../jni_bindings"
    libname = "liveview_native_core"
    targets = ["arm", "x86"]
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'org.phoenixframework'
                artifactId = 'liveview-native-core-jetpack'
                version = '0.1.0-pre-alpha04'
                from components.release
            }
        }
    }
}